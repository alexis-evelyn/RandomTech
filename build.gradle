plugins {
	id 'fabric-loom' version '0.5-SNAPSHOT' // ./gradlew build - For Building The Mod
	id 'io.codearte.nexus-staging' version '0.22.0' // ./gradlew closeAndReleaseRepository - For releasing uploaded releases to the public
	id 'com.matthewprenger.cursegradle' version '1.4.0' // ./gradlew curseforge - For releasing mods to Curseforge
	id 'maven-publish' // ./gradlew publish - For publishing to Sonatype
	id 'signing' // Performed By Maven Publish - For Signing Artifacts Before Publishing to Sonatype
//	id "com.github.spotbugs" version '4.5.0'
}

logger.lifecycle "(Root) Running Gradle Version: $gradle.gradleVersion"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
group = project.maven_group

//main_version = project.mod_version
//current_date = new Date()
//snapshot_version = "${main_version}-${current_date}-SNAPSHOT"
version = "${project.mod_version}"

// Tells Gradle to Wait for the API to Remap Before Remapping the Root Mod
remapJar.dependsOn ':api:remapJar'

minecraft {
	// Used to make private methods/classes/fields public
	accessWidener "src/main/resources/randomtech.accesswidener"
}

//spotbugs {
//	ignoreFailures = true
//}

configurations {
	deployerJars
}

repositories {
	maven {
		name "Patchouli"
		url "https://maven.blamejared.com/"
	}
	ivy {
		name "Github Repos"
		url "https://jitpack.io/"

		// This exists to force Gradle to ignore bad versioning information from commit based builds on JitPack
		metadataSources {
			artifact() // Look directly for artifact
		}

		layout "maven"
		patternLayout {
			artifact "[organisation]/[module]/[revision]/[artifact]-[revision].[ext]"
			artifact "[organisation]/[module]/[revision]/[artifact]-[revision]-dev.[ext]"
			m2compatible = true
		}
	}
	maven {
		name "NBT Crafting"
		url "https://maven.siphalor.de"
	}
	maven {
		name "Hwyla"
		url "https://maven.tehnut.info"
	}
	flatDir {
		name "Local API Build"
		dirs "api/build/libs"
	}
}

dependencies {
	deployerJars "io.packagecloud.maven.wagon:maven-packagecloud-wagon:0.0.6"

	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// For annotations
	implementation "org.jetbrains:annotations:${project.jetbrains_annotation_version}"
	implementation "javax.annotation:javax.annotation-api:${project.javax_annotation_version}" // Cause Some Dependency (Fiber DSL For Kotlin) Needs It

	// For Loom - https://github.com/FabricMC/fabric-loom/issues/280
	implementation "org.eclipse.platform:org.eclipse.equinox.registry:${project.eclipse_extension_registry_support_version}"

//	// For Spotbugs Annotations
//	compileOnly "com.github.spotbugs:spotbugs-annotations:${spotbugs.toolVersion.get()}"
//
//	// For Spotbugs Plugin
//	spotbugs "com.github.spotbugs:spotbugs:${spotbugs.toolVersion.get()}"

	// RandomTech API - https://docs.gradle.org/current/userguide/multi_project_builds.html
	implementation project(path: ':api') // ./gradlew :api:build -> ./api/build/libs/alexisevelyn-api-0.0.4-SNAPSHOT.jar
	include (name: 'alexisevelyn-api', version: "${project(":api").mod_version}", ext: 'jar') // Retrieves the version info from API's mod_version var and includes the remapped API jar into the root jar
	
	// Reborn Core
	modImplementation ("RebornCore:RebornCore-${project.reborn_core_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}

	// Reborn's Energy API
	modImplementation ("teamreborn:energy:${project.reborn_energy_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}

	// Mod Menu
	modImplementation ("io.github.prospector:modmenu:${project.mod_menu_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}

	// AutoConfig for Use With Mod Menu
	modImplementation ("me.sargunvohra.mcmods:autoconfig1u:${project.autoconf_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}
	include "me.sargunvohra.mcmods:autoconfig1u:${project.autoconf_version}"

	// Hywla
	// modImplementation "curse.maven:hywla:${project.hywla_curseforge_version}" // Same fix as on Patchouli. the fix was that loom currently cannot handle depending on artifacts with the same ID but different classifier (regular patchouli jar vs :api)
	modCompileOnly ("mcp.mobius.waila:Hwyla:${project.hywla_version}") { // Used to be modImplementation, but kept causing issues with dev server
		exclude(group: "net.fabricmc.fabric-api")
	}

	// Roughly Enough Items
	modImplementation ("me.shedaniel:RoughlyEnoughItems:${project.rei_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}

	// Patchouli API
	modImplementation ("vazkii.patchouli:Patchouli:${project.patchouli_version}") { // Note: https://discordapp.com/channels/507304429255393322/507982478276034570/745174055383990372
		exclude(group: "net.fabricmc.fabric-api")
	}

	// For Shaders - https://github.com/Ladysnake/Satin/blob/master/README.md
	modImplementation ("io.github.ladysnake:Satin:${project.satin_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}
	include "io.github.ladysnake:Satin:${project.satin_version}"

	// For NBT Crafting - https://github.com/Siphalor/nbt-crafting/blob/1.16-2.0/README.md
	modImplementation ("de.siphalor:nbtcrafting-1.16:${project.nbt_crafting_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}
	include "de.siphalor:nbtcrafting-1.16:${project.nbt_crafting_version}"

	// For Animation - Wait Until 3.0 For Block Entity Animation (https://discordapp.com/channels/730912704776110121/730913776307535922/753987515740061888)
	modImplementation ("com.github.bernie-g:geckolib:${project.geckolib_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}
	include ("com.github.bernie-g:geckolib:${project.geckolib_version}")

	// PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
	// You may need to force-disable transitiveness on them.
}

processResources {
	inputs.property "version", project.version

	from(sourceSets.main.resources.srcDirs) {
		include "fabric.mod.json"
		expand "version": project.version
	}

	from(sourceSets.main.resources.srcDirs) {
		exclude "fabric.mod.json"
	}
}

// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

task javadocs(type: Javadoc) {
	source = sourceSets.main.allJava
}

task javadocsJar(type: Jar, dependsOn: javadocs) {
	classifier = "javadoc"
	javadocs.failOnError false
	from javadocs.destinationDir
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE.md"
}

// https://docs.gradle.org/current/userguide/build_lifecycle.html
gradle.taskGraph.beforeTask { Task task ->
	logger.lifecycle "(Root) Executing $task..."
}

gradle.taskGraph.afterTask { Task task, TaskState state ->
	if (state.failure) {
		logger.lifecycle "(Root) FAILED $task..."
	} else {
		logger.lifecycle "(Root) COMPLETED $task..."
	}
}

// Apply Publishing Gradle File
apply from: 'publishing.gradle'

// Separating Runtime Only Dependencies From Other Dependencies
apply from: 'runtime-only-dependencies.gradle'

// Separating Unit Testing Only Dependencies From Other Dependencies
apply from: 'unit-testing-only-dependencies.gradle'

// For all projects. This includes subprojects and our project.
//allprojects {
//	// I believe it includes parent projects too, but verify that.
//}

// For Any General Subprojects
//subprojects {}

// Specifically the API Subproject
project(':api') {
	apply plugin: 'fabric-loom'
	apply plugin: 'com.matthewprenger.cursegradle'
}

// Specifically for this project?
//project(":") {}

/* https://discordapp.com/channels/507304429255393322/507982478276034570/745174375753449543
 *
 * modCompile - outdated, don't use.
 * modImplementation -> if someone depends on YOUR mod, they get this dependency as runtime dependency, but can't reference its code
 * modApi -> if someone depends on your mod, they get this dependency as compile time dependency, and can reference its code directly
 */